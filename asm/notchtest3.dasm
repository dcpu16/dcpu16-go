; Notch's second "hello word" program.
; http://i.imgur.com/XIXc4.jpg
; Supposed to show formatting.
:start
  SET I, 0
  SET J, 0
  SET B, 0xf100
:nextchar
     SET A, [data+I]
     IFE A, 0
        SET PC, end
     IFG A, 0xff
        SET PC, setcolor
     BOR A, B
     SET [0x8000+J], A
     ADD I, 1
     ADD J, 1
     SET PC, nextchar
:setcolor
     SET B, A
     AND B, 0xff
     SHL B, 8
     IFG A, 0x1ff
         ADD B, 0x80 ; Add high bit to each character we color. So the character must be 7-bit ASCII
     ADD I, 1
     SET PC, nextchar
:data
    dat 0x170, "Hello ", 0x2e1, "world", 0x170, ", how are you?", 0
    ; Color format:
    ; After processing:
    ; 0x170 -> b = 0x7000 -> 0111 0000 0XXX XXXX = white(grey) on black
    ; 0x2e1 -> b = 0xe180 -> 1110 0001 1XXX XXXX = yellow on blue
    ; b gets OR'd with each character.
    ; ANSI says: black is 0, white is 7, yellow is 3, blue is 4
    ; If black is 0 and grey is 7, it's <FORE> <BACK> <EXTRA BIT> or <FORE> <EXTRA BIT> <BACK>
:end
    SET PC, start
